use open Util
use Ord
use Fn
use Option
use List
use Int
use Stream
use Ref

module TreeSet :> sig
  %% Immutable set.
  type t :: o -> o

  %% Creates an empty set.
  val persistent ~empty 'a : t 'a

  %% Returns the number of elements in the given set.
  val persistent ~size 'a : t 'a -> int

  %% Returns [true] if the given set is empty.
  val persistent ~is-empty 'a : t 'a -> bool

  %% [x |> mem ord s] returns [true] if [s] contains [x].
  val persistent ~mem 'a : implicit (Ord.t 'a) -> t 'a -> 'a -> bool

  %% [contains ord x s] is the same as [mem ord s x].
  val persistent ~contains 'a : implicit (Ord.t 'a) -> 'a -> t 'a -> bool

  %% [get ord x s] Returns an element that is equal to [x] with respect to [ord].
  val persistent ~get 'a : implicit (Ord.t 'a) -> 'a -> t 'a -> option 'a

  %% Adds a value to the given set. If the set contains a value that is equal to the new value,
  %% the old value is replaced with the new value. If you need to get the old value, query [get] first.
  val persistent ~insert 'a : implicit (Ord.t 'a) -> 'a -> t 'a -> t 'a

  %% Removes a value from the given set.
  val persistent ~remove 'a : implicit (Ord.t 'a) -> 'a -> t 'a -> t 'a

  %% Constructs a set from a list. If the list contains two equal values, the preceding one is preferred.
  val persistent ~of-list 'a : implicit (Ord.t 'a) -> list 'a -> t 'a

  val persistent ~to-list 'a : t 'a -> list 'a

  val persistent ~to-stream 'a : t 'a -> Stream.t 'a
end = struct

  % AVL-tree's invariant:
  % (INV) for each node n, |height(n#left) - height(n#right)| < 2

  % AVL tree
  type node 'a = Node of (option (|
    height : int,
    value : 'a,
    left : node 'a,
    right : node 'a,
  |))

  type t 'a = Tree of (|
    root : node 'a,
    size : int,
  |)

  val persistent ~empty-node = Node(None)

  val persistent ~empty = Tree (|
    root = empty-node,
    size = 0,
  |)

  val persistent ~size (Tree t) = t#size

  val persistent ~is-empty-node (Node(n)) = Option.is-none n

  val persistent ~is-empty t = size t == 0

  val persistent ~get ord x (Tree t) =
    t#root |> Fn.fix (fun recf (Node(node)) -> (
      match node with
      | None -> None
      | Some(node) ->
        match Ord.compare ord x node#value with
        | Ord.Eq -> Some(node#value)
        | Ord.Lt -> recf node#left
        | Ord.Gt -> recf node#right
        end
      end
    ))

  val persistent ~mem ord s x = Option.is-some (get ord x s)

  val persistent ~contains ord x s = mem ord s x

  val persistent ~height (Node n) =
    match n with
    | None -> 0
    | Some(node) -> node#height
    end

  val persistent ~make-node left right value =
    Node (Some (|
      height = 1 + Int.max (height left) (height right),
      left = left,
      right = right,
      value = value,
    |))

  % If both l and r satisfy (INV) and |height(l) - height(r)| <= 2, so does [balance l r v].
  val persistent ~balance l r v =
    let hl = height l in
    let hr = height r in
    if Int.(abs (hl - hr) < 2) then
      make-node l r v
    else if hl > hr + 1 then
      let (Node(Some(l))) = l in
      let lv = l#value in
      let ll = l#left in
      let lr = l#right in
      if height ll >= height lr then
        %        v              lv
        %      /   \           /  \
        %     lv    r   ->   ll     v
        %    /  \                 /   \
        %   ll  lr               lr    r
        make-node ll (make-node lr r v) lv
      else
        let (Node(Some(lr))) = lr in
        let lrv = lr#value in
        let lrl = lr#left in
        let lrr = lr#right in
        %        v                 v               lrv
        %      /   \             /   \            /    \
        %     lv    r   ->     lrv    r   ->    lv      v
        %    /  \             /   \            /  \    /  \
        %   ll  lrv          lv   lrr         ll  lrl lrr  r
        %      /   \        /  \
        %    lrl   lrr     ll  lrl
        make-node (make-node ll lrl lv) (make-node lrr r v) lrv
    else
      let (Node(Some(r))) = r in
      let rv = r#value in
      let rl = r#left in
      let rr = r#right in
      if height rr >= height rl then
        %        v             rv
        %      /   \          /   \
        %     l    rv   ->   v     rr
        %         /  \      /  \
        %        rl  rr    l   rl
        make-node (make-node l rl v) rr rv
      else
        let (Node(Some(rl))) = rl in
        let rlv = rl#value in
        let rll = rl#left in
        let rlr = rl#right in
        %        v              v               rlv
        %      /   \          /   \            /   \
        %     l    rv   ->   l    rlv   ->    v     rv
        %         /  \           /   \       / \    / \
        %       rlv  rr         rll  rv     l  rll rlr rr
        %      /   \                /  \
        %    rll   rlr            rlr   rr
        make-node (make-node l rll v) (make-node rlr rr rv) rlv

  val persistent ~insert ord x (Tree t) =
    let found = Ref.make false in
    let root = t#root |> Fn.fix (fun recf (Node(node)) -> (
      match node with
      | None -> make-node empty-node empty-node x
      | Some(node) ->
        match Ord.compare ord x node#value with
        | Ord.Lt -> balance (recf node#left) node#right node#value
        | Ord.Gt -> balance node#left (recf node#right) node#value
        | Ord.Eq ->
          let () = found |> Ref.set true in
          make-node node#left node#right x
        end
      end
    )) in
    let size =
      if Ref.get found then
        t#size
      else
        t#size + 1 in
    Tree (| root = root, size = size |)

  % pop the minimum value from the tree
  val persistent ~rec pop (Node(n)) =
    match n with
    | None -> Option.none
    | Some(n) ->
      if is-empty-node (n#left) then
        Option.some (n#value, n#right)
      else
        pop n#left |> Option.map (fun (v, l) -> (
          (v, balance l n#right n#value)
        ))
    end

  val persistent ~remove ord x (Tree t) =
    let found = Ref.make false in
    let root = t#root |> Fn.fix (fun recf (Node(node)) -> (
      match node with
      | None -> empty-node
      | Some(node) ->
        match Ord.compare ord x node#value with
        | Ord.Lt -> balance (recf node#left) node#right node#value
        | Ord.Gt -> balance node#left (recf node#right) node#value
        | Ord.Eq ->
          let () = found |> Ref.set true in
          if is-empty-node node#left then
            node#right
          else if is-empty-node node#right then
            node#left
          else
            let (Some((v, r))) = pop node#right in
            balance node#left r v
        end
      end
    )) in
    let size =
      if Ref.get found then
        t#size - 1
      else
        t#size in
    Tree (| root = root, size = size |)

  val persistent ~of-list ord list =
    list |> List.fold-right (fun x s -> (s |> insert ord x)) empty

  % BFS-order
  val persistent ~to-stream (Tree(t)) =
    [t#root] |> Stream.unfold (fun ns -> (
      ns |> Fn.fix (fun loop ns -> (
        match ns with
        | [] -> None
        | (Node(n)) :: ns ->
          match n with
          | None ->
            loop ns
          | Some(n) ->
            let next = List.append ns [n#left, n#right] in
            Some((n#value, next))
          end
        end
      ))
    ))

  val persistent ~to-list s = (Fn.compose Stream.to-list to-stream) s
end
