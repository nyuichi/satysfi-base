use open Util
use Ord
use TreeSet
use Option
use List
use Int
use Fn
use Stream
use Tuple

module TreeMap :> sig
  %% Immutable map.
  type t :: o -> o -> o

  %% Creates an empty map.
  val persistent ~empty 'k 'v : t 'k 'v

  %% Returns the number of elements in the given map.
  val persistent ~size 'k 'v : t 'k 'v -> int

  %% Returns [true] if the given map is empty.
  val persistent ~is-empty 'k 'v : t 'k 'v -> bool

  %% Returns [true] if the given map contains a binding to the given key.
  val persistent ~contains 'k 'v : implicit (Ord.t 'k) -> 'k -> t 'k 'v -> bool

  %% [get ord k m] returns the value bound to [k].
  val persistent ~get 'k 'v : implicit (Ord.t 'k) -> 'k -> t 'k 'v -> option 'v

  %% Binds key to the value.
  val persistent ~insert 'k 'v : implicit (Ord.t 'k) -> 'k -> 'v -> t 'k 'v -> t 'k 'v

  %% Removes a binding from the given map.
  val persistent ~remove 'k 'v : implicit (Ord.t 'k) -> 'k -> t 'k 'v -> t 'k 'v

  %% Constructs a map from a list. If the list contains two equal keys, the preceding one is preferred.
  val persistent ~of-list 'k 'v : implicit (Ord.t 'k) -> list ('k * 'v) -> t 'k 'v

  val persistent ~to-list 'k 'v : t 'k 'v -> list ('k * 'v)

  val persistent ~to-stream 'k 'v : t 'k 'v -> Stream.t ('k * 'v)
end = struct

  type t 'k 'v = TreeSet.t ('k * (option 'v))

  val persistent ~empty = TreeSet.empty

  val persistent ~size = TreeSet.size

  val persistent ~is-empty = TreeSet.is-empty

  val persistent ~key-ord ord = Ord.make (fun (k1, _) (k2, _) -> Ord.compare ord k1 k2)

  val persistent ~get ord key map =
    map |> TreeSet.get (key-ord ord) (key, None) |> Option.map (Fn.compose Option.unwrap Tuple.Pair.snd)

  val persistent ~contains ord key map =
    Option.is-some (get ord key map)

  val persistent ~insert ord key value map =
    map |> TreeSet.insert (key-ord ord) (key, Some(value))

  val persistent ~remove ord key map =
    map |> TreeSet.remove (key-ord ord) (key, None)

  val persistent ~of-list ord list =
    list |> List.map (fun (k, v) -> (k, Some(v))) |> TreeSet.of-list (key-ord ord)

  val persistent ~to-stream map =
    map |> TreeSet.to-stream |> Stream.map (fun (k, v) -> (k, Option.unwrap v))

  val persistent ~to-list map =
    map |> to-stream |> Stream.to-list

end
